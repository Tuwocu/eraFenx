;_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/#
;
;	Module		:VAR_FUNC.ERB
;	Facility	:関数置き場(バリアント本体)。先頭に"VAR_"を付与。
;
;	Licence		:ライセンスフリー。
;
;	Modification Data:
;
;	Edit	Date			Author					Reason
;	001		20XX/XX/XX		----					新規作成
;
;_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/#

;===============================================================================
;  ◇記載関数一覧(記載順)
;
;		Module Name						Explanation
;
; *- xxxx
;
;	・ VAR_ADD_CHARA					VAR:角色追加
;	・ VAR_PENIS						VAR:男性器
;	・ VAR_WOMAN						VAR:女性
;	・ VAR_SEX							VAR:性別
;	・ VAR_HETEROSEX					VAR:異性愛
;	・ VAR_BAR_BASE						VAR:バー出力(基礎)
;	・ VAR_MONEY_ADD					VAR:資金加算
;	・ VAR_PRINT_LIST_NAME				VAR:表示(リスト用名称)
;	・ VAR_RESET_VIRGIN					VAR:处女化
;	・ VAR_RESET_BUST					VAR:初期化(バストサイズ)
;	・ VAR_バストサイズ					VAR:バストサイズ
;	・ VAR_MOOD_LV						VAR:心情LV
;
; *- 制御系
;
;	・ VAR_MAKE_LIST					VAR:リスト更新
;	・ VAR_MAKE_PL						VAR:プレイヤーリスト作成
;	・ VAR_MAKE_CL						VAR:角色リスト作成
;	・ VAR_GET_CL						VAR:角色リスト取得
;	・ VAR_SET_TARGET					VAR:ターゲット設定
;
;===============================================================================

;-------------------------------------------------------------------------------
;   xxxx
;-------------------------------------------------------------------------------

;=================================================
;   sub VAR_ADD_CHARA
;=================================================
;   VAR:角色追加
;-------------------------------------------------
; Input:
;  ARG:0				対象角色
;-------------------------------------------------
@VAR_ADD_CHARA(ARG)
#DIM L_T

L_T = ARG

;-----------------------------
;- 共通編集
;-----------------------------

MAXBASE:L_T:体力 = 200
BASE:L_T:体力 = MAXBASE:L_T:体力
MAXBASE:L_T:绝顶 = FLAG:最大值_绝顶
MAXBASE:L_T:心情 = FLAG:最大数_心情

CFLAG:L_T:使用口上 = NO:L_T
CFLAG:L_T:危険日 = DAY % 7

;初期化
CSTR:L_T:角色紹介0 =
CSTR:L_T:角色紹介1 =
CSTR:L_T:角色紹介2 =
CSTR:L_T:角色紹介3 =

SIF CALLNAME:L_T == ""
	CALLNAME:L_T = %NAME:L_T%

;-----------------------------
;- その他
;-----------------------------

CSTR:L_T:劳动服装名 = 无


;=================================================
;   sub VAR_PENIS
;=================================================
;   VAR:男性器
;-------------------------------------------------
; Input:
;  ARG:0				対象者
; Output:
;  RETURNF				真偽值
;-------------------------------------------------
@VAR_PENIS(ARG=-1)
#FUNCTION

ARG  = ( (ARG < 0) ? TARGET # ARG)

;- CHK:性別
IF !VAR_SEX(ARG) || TALENT:ARG:ふたなり
	;- 有り
	RETURNF 1
ELSE
	;- 無し
	RETURNF 0
ENDIF

;=================================================
;   sub VAR_WOMAN
;=================================================
;   VAR:女性
;-------------------------------------------------
;   女 or 伪娘なら真
;-------------------------------------------------
; Input:
;  ARG:0				対象者
;  ARG:1				CSV優先(0:角色優先 1:CSV優先)
; Output:
;  RETURNF				性別区分(0:男性 1:女 or 伪娘)
;-------------------------------------------------
@VAR_WOMAN(ARG=-1, ARG:1=0)
#FUNCTION
#DIM L_TMP
#DIM L_OPT
ARG  = ( (ARG < 0) ? TARGET # ARG)

IF ARG:1
	L_TMP = CSVTALENT(ARG,GETNUM(TALENT,"男性"),0)
	L_OPT = CSVTALENT(ARG,GETNUM(TALENT,"伪娘"),0)
ELSE
	L_TMP = TALENT:ARG:男性
	L_OPT = TALENT:ARG:伪娘
ENDIF

;- CHK:性別
IF !L_TMP || L_OPT
	;- 女 or 伪娘
	RETURNF 1
ELSE
	;- 男性
	RETURNF 0
ENDIF


;=================================================
;   sub VAR_SEX
;=================================================
;   VAR:性別
;-------------------------------------------------
; Input:
;  ARG:0				対象者
;  ARG:1				CSV優先(0:角色優先 1:CSV優先)
; Output:
;  RETURNF				性別区分(0:男性 1:女性)
;-------------------------------------------------
@VAR_SEX(ARG=-1, ARG:1=0)
#FUNCTION
#DIM L_TMP
ARG  = ( (ARG < 0) ? TARGET # ARG)

IF ARG:1
	L_TMP = CSVTALENT(ARG,GETNUM(TALENT,"男性"),0)
ELSE
	L_TMP = TALENT:ARG:男性
ENDIF

;- CHK:性別
IF !L_TMP
	;- 女性
	RETURNF 1
ELSE
	;- 男性
	RETURNF 0
ENDIF

;=================================================
;   sub VAR_HETEROSEX
;=================================================
;   VAR:異性愛
;-------------------------------------------------
; Input:
;  ARG:0				対象者
;  ARG:1				実行者
; Output:
;  RETURNF				性別区分(0:異性 1:同性(百合) 2:同性(ＢＬ))
;-------------------------------------------------
@VAR_HETEROSEX(ARG,ARG:1)
#FUNCTION
#DIM L_P
#DIM L_T

;- 性別取得
L_P = VAR_SEX(ARG)
L_T = VAR_SEX(ARG:1)

;- CHK:性別
IF L_P == L_T
;同性の場合
	IF L_P
		RETURNF 1
	ELSE
		RETURNF 2
	ENDIF
ELSE
;異性の場合
	RETURNF 0
ENDIF



;=================================================
;   sub VAR_BAR_BASE
;=================================================
;   VAR:バー出力(基礎)
;-------------------------------------------------
; Input:
;  ARG:0				対象角色
;  ARG:1				対象項目(基礎)
;  ARG:2				位数区分(1:16 2:4 その他:6)
;-------------------------------------------------
@VAR_BAR_BASE(ARG, ARG:1, ARG:2 = 0)
VARSET LOCAL
LOCAL:0 = BASE:ARG:(ARG:1)
LOCAL:1 = MAXBASE:ARG:(ARG:1)
SIF LOCAL:0 < 0
	LOCAL:0 = 0

SELECTCASE ARG:2
	CASE 1
		LOCAL:2=16
	CASE 2
		LOCAL:2=4
	CASEELSE
		LOCAL:2=6
ENDSELECT

SIF LOCAL:1 <= 0
	RETURN

;- 位算出(最大に合わせる)
WHILE 1
	SIF LOCAL:1 < POWER(10,LOCAL:9)
		BREAK
	LOCAL:9 ++
WEND

PRINTFORM %BASENAME:(ARG:1)%
BAR LOCAL:0,LOCAL:1,LOCAL:2
PRINTFORM (%TOSTR(LOCAL:0),LOCAL:9%/{LOCAL:1})


;=================================================
;   sub VAR_MONEY_ADD
;=================================================
;   VAR:資金加算
;-------------------------------------------------
; Input:
;  ARG:0				加算值
;-------------------------------------------------
@VAR_MONEY_ADD(ARG)

MONEY				+= ARG
FLAG:累计获得资金	 = MIN(FLAG:累计获得资金 + ARG, 99999)


;=================================================
;   sub VAR_PRINT_LIST_NAME
;=================================================
;   VAR:表示(リスト用名称)
;-------------------------------------------------
; Input:
;  ARG:0				角色
;-------------------------------------------------
@VAR_PRINT_LIST_NAME(ARG)
CALL INFO_TARGET_DELETE(ARG)
PRINTFORM %CALLNAME:ARG, 18, LEFT%


;=================================================
;   sub VAR_RESET_VIRGIN
;=================================================
;   VAR:处女化
;-------------------------------------------------
; Input:
;  ARG:0				角色
;-------------------------------------------------
@VAR_RESET_VIRGIN(ARG=-1)
SIF ARG == -1
	ARG = TARGET

TALENT:ARG:非处女 = 0
TALENT:ARG:A非处女 = 0
ABL:ARG:V感觉 = 0
EXP:ARG:V经验 = 0
EXP:ARG:性交经验 = 0
EXP:ARG:接吻经验 = 0


;=================================================
;   sub VAR_RESET_BUST
;=================================================
;   VAR:初期化(バストサイズ)
;-------------------------------------------------
; Input:
;  ARG:0				角色
;-------------------------------------------------
@VAR_RESET_BUST(ARG)
TALENT:ARG:绝壁 = 0
TALENT:ARG:贫乳 = 0
TALENT:ARG:巨乳 = 0
TALENT:ARG:爆乳 = 0
TALENT:ARG:超乳 = 0

;=================================================
;   sub VAR_バストサイズ
;=================================================
;   VAR:バストサイズ
;-------------------------------------------------
; Input:
;  ARG:0				角色
; Output:
;  RETURNF				サイズ(绝壁:-2 贫乳:-1 普乳:0 巨乳:1 爆乳:2 C:3)
;-------------------------------------------------
@VAR_バストサイズ(ARG=-1)
#FUNCTION
SIF ARG == -1
	RETURNF 0

IF TALENT:ARG:绝壁
	RETURNF -2
ELSEIF TALENT:ARG:贫乳
	RETURNF -1
ELSEIF TALENT:ARG:巨乳
	RETURNF 1
ELSEIF TALENT:ARG:爆乳
	RETURNF 2
ELSEIF TALENT;ARG:超乳
    RETURNF 3
ELSE
	RETURNF 0
ENDIF

;=================================================
;   sub VAR_MOOD_LV
;=================================================
;   VAR:心情LV
;-------------------------------------------------
; Input:
;  ARG:0				対象角色
; Output:
;  RETURNF				心情レベル(最大值は位数)
;-------------------------------------------------
@VAR_MOOD_LV(ARG)
#FUNCTION
#DIM L_VAL
#DIM L_LV

L_VAL=0
L_LV=0

;- 計算:位単位
L_VAL = (FLAG:最大数_心情 / FLAG:最大数_心情位数)

;- 計算:レベル
L_LV = (BASE:ARG:心情 / L_VAL)

;- 調整
L_LV = MIN(L_LV, FLAG:最大数_心情位数)

RETURNF L_LV


;-------------------------------------------------------------------------------
;   制御系
;-------------------------------------------------------------------------------

;=================================================
;   sub VAR_MAKE_LIST
;=================================================
;   VAR:リスト更新
;-------------------------------------------------
@VAR_MAKE_LIST

GTR_CHARA_CNT = CHARANUM

;- VAR:プレイヤーリスト作成
CALL VAR_MAKE_PL

;- VAR:角色リスト作成
CALL VAR_MAKE_CL


;=================================================
;   sub VAR_MAKE_PL
;=================================================
;   VAR:プレイヤーリスト作成
;-------------------------------------------------
;   调教に参加する角色(プレイヤー)のリストを作成する
;-------------------------------------------------
@VAR_MAKE_PL
#DIM L_I
#DIM L_CNT
#DIM L_TGT

VARSET GTR_PL, -1
FLAG:调教对象 = 0

L_TGT = -1

L_CNT = 0
FOR L_I, 0, CHARANUM
	;- 除外
	IF L_I == MASTER
		;主人は許可
	ELSEIF !CFLAG:L_I:调教对象旗标
		CONTINUE
	ENDIF

	;- 残った角色を保存
	GTR_PL:L_CNT = L_I

	SIF L_I != MASTER
		FLAG:调教对象 ++

	SIF (L_TGT < 0) && (L_I != MASTER)
		L_TGT = L_I

	L_CNT ++
NEXT

SIF TARGET != L_TGT
	TARGET = L_TGT


;=================================================
;   sub VAR_MAKE_CL
;=================================================
;   VAR:角色リスト作成
;-------------------------------------------------
;   表示対象の角色のリストを作成する
;-------------------------------------------------
@VAR_MAKE_CL
#DIM L_I
#DIM L_CNT

VARSET GTR_CL, -1

;旗标類初期化
FLAG:陷落人数 = 0

L_CNT = 0
FOR L_I, 0, CHARANUM
	;- 除外

	;旗标類カウント
	SIF TALENT:L_I:上位陷落
		FLAG:陷落人数 ++

	;- 残った角色を保存
	GTR_CL:L_CNT = L_I
	L_CNT ++
NEXT

;=================================================
;   sub VAR_GET_CL
;=================================================
;   VAR:角色リスト取得
;-------------------------------------------------
; Input:
;  ARGS					处理(NEXT:次 PREV:前)
;  ARG:0				対象角色
; Output:
;  RETURNF				結果(該当角色、または-1)
;-------------------------------------------------
@VAR_GET_CL(ARGS, ARG)
#FUNCTION
#DIM L_I
#DIM L_TMP

L_TMP = FINDELEMENT(GTR_CL, ARG)
SIF L_TMP == -1
	RETURNF -1

SELECTCASE ARGS
	CASE "PREV"
		SIF !L_TMP
			RETURNF -1
		RETURNF GTR_CL:(L_TMP-1)

	CASE "NEXT"
		RETURNF GTR_CL:(L_TMP+1)
	;ERROR
	CASEELSE
		RETURNF -1
ENDSELECT


;=================================================
;   sub VAR_SET_TARGET
;=================================================
;   VAR:ターゲット設定
;-------------------------------------------------
; Input:
;  ARG:0				対象角色
; Output:
;  RETURNF				結果(0:OK 1:男女混合不可 2:体型エラー)
;-------------------------------------------------
@VAR_SET_TARGET(ARG)
#FUNCTION
#DIM L_I
#DIM L_T
#DIM L_P
#DIM L_HIT
#DIM L_SEX

L_P = ARG
L_HIT=0
L_SEX = VAR_SEX(ARG)

;体型チェック
IF (TALENT:L_P:小人 && !L_SEX) || TALENT:L_P:巨人 || TALENT:L_P:非人型
	L_HIT = 2
	GOTO CHKEND
ENDIF

FOR L_I,0,DEFTR_PL_NUM
	L_T = GTR_PL:L_I
	SIF L_T < 0
		BREAK
	SIF L_T == MASTER
		CONTINUE
	SIF L_T == L_P
		CONTINUE
	;性別が同じ
	SIF VAR_SEX(L_T) == L_SEX
		CONTINUE
	L_HIT = 1
	BREAK
NEXT

IF !L_HIT
	;- MASTER以外なら、規定人数までは自動でTARGETに
	IF MASTER != L_P
		IF FLAG:调教对象 < FLAG:最大数_调教人数
			CFLAG:L_P:调教对象旗标 = 1
			FLAG:调教对象 ++
		ENDIF
	ENDIF
ENDIF

$CHKEND
RETURNF L_HIT
