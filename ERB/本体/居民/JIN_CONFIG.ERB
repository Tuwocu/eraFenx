;-------------------------------------------------
; 居民设定
;-------------------------------------------------
@JN_CONFIG
#DIM L_INPUT

$INPUT_LOOP2
CLEARLINE LINECOUNT

DRAWLINE
PRINTFORML 请选择要变更的项目。
PRINTFORML [0] - てすと
PRINTFORML [1] - 正式居民设定(一部事件发生)
PRINTFORML [2] - 特別保护设定(被害保护)
PRINTFORML [3] - 各居民事件倾向
PRINTFORML [4] - 交流对象
PRINTFORML [5] - 服装变更
PRINTFORML [6] - 职务变更
PRINTFORML
PRINTFORML [50] - 姓名变更
PRINTFORML
PRINTFORML
PRINTFORML [100] - 居民抹消
PRINTFORML
PRINTFORML [999] - 返回

$INPUT_LOOP
INPUT
L_INPUT = RESULT

SELECTCASE L_INPUT
CASE 0
	CALL MANAGE_CHARA_SELECT("MNG_正式居民指定")
CASE 1
	CALL JN_CONFIG_REGULAR
CASE 2
	CALL JN_CONFIG_T_BLOCK
CASE 3
	CALL JN_CONFIG_EVENT
CASE 4
	CALL JN_CONFIG_COM
CASE 5
	CALL COSTUME_MAIN
CASE 6
	CALL POSITION_CHANGE
CASE 50
	CALL JN_CONFIG_N_C
CASE 100
	CALL JN_CONFIG_DELETE
CASE 999
	RETURN
CASEELSE
	GOTO INPUT_LOOP
ENDSELECT

RESTART




;-------------------------------------------------
; 正式居民设定
;-------------------------------------------------
@JN_CONFIG_REGULAR(ARG:0,ARG:1,ARGS:0)
#DIM L_INPUT
#DIM L_INPUT2
#DIM L_I
#DIM L_REGULAR
$DATA_LIST
L_INPUT = 0
L_INPUT2 = 0
L_REGULAR = 0
L_I = 0

DRAWLINE
PRINTFORML 请选择要划定为正式居民的居民(最多50人)
DRAWLINE
SETCOLOR 0,250,250
PRINTFORM 【正式居民:
ARG:0 = 531
ARG:1 = 1
CALL MEMBER_LIST2(ARG:0,ARG:1)
L_REGULAR = FLAG:泛用变数
PRINTFORML 】
RESETCOLOR


FOR L_I,0,CHARANUM
	CSTR:L_I:4999 '= \@(CFlag:L_I:正式居民) ? o # x\@ + " "
NEXT
ARGS:0 = 正式居民
CALL LIST_CHARA(ARGS:0,,,4999)

L_INPUT = RESULT

IF L_INPUT != 1000
	IF TALENT:L_INPUT:NPC == 999
		PRINTFORMW 不能选择NPC
		GOTO DATA_LIST
	ELSEIF L_INPUT == 0
		PRINTFORMW 不能选择你自己
		GOTO DATA_LIST
	ELSEIF L_REGULAR >= 50 && CFlag:L_INPUT:正式居民 == 0
		PRINTFORMW 正式居民已经到达50人
		GOTO DATA_LIST
	ELSEIF CFlag:L_INPUT:正式居民 == 0
		CFlag:L_INPUT:正式居民 = 1
		PRINTFORMW 将%CALLNAME:L_INPUT%划定为正式居民
		GOTO DATA_LIST
	ELSEIF CFlag:L_INPUT:正式居民 == 1
		CFlag:L_INPUT:正式居民 = 0
		PRINTFORMW 取消%CALLNAME:L_INPUT%的正式居民的身份
		GOTO DATA_LIST
	ENDIF
ENDIF

; CALL JN_CONFIG



;-------------------------------------------------
; 特別保护设定
;-------------------------------------------------
@JN_CONFIG_T_BLOCK(ARG:0,ARG:1,ARGS:0)
#DIM L_INPUT
#DIM L_INPUT2
#DIM L_I
#DIM L_REGULAR
$DATA_LIST
L_INPUT = 0
L_INPUT2 = 0
L_REGULAR = 0
L_I = 0

DRAWLINE
PRINTFORML 请选择要变更特别保护设定的居民(最多50人)
PRINTFORML 受到特别保护的居民将不会发生不利，被害之类的事件。
DRAWLINE
SETCOLOR 0,250,250
PRINTFORM 【保护:
ARG:0 = 532
ARG:1 = 1
CALL MEMBER_LIST2(ARG:0,ARG:1)
L_REGULAR = FLAG:泛用变数
PRINTFORML 】
RESETCOLOR
DRAWLINE




FOR L_I,0,CHARANUM
	CSTR:L_I:4999 '= \@(CFlag:L_I:特別保护) ? o # x\@ + " "
NEXT
ARGS:0 = 特別保护
CALL LIST_CHARA(ARGS:0,,,4999)

L_INPUT = RESULT

IF L_INPUT != 1000
	IF TALENT:L_INPUT:NPC == 999
		PRINTFORMW 不能选择NPC
		GOTO DATA_LIST
	ELSEIF L_INPUT == 0
		PRINTFORMW 不能选择你自己
		GOTO DATA_LIST
	ELSEIF L_REGULAR >= 50&& CFlag:L_INPUT:特別保护 == 0
		PRINTFORMW 已有50人受到特别保护了
		GOTO DATA_LIST
	ELSEIF CFlag:L_INPUT:特別保护 == 0
		CFlag:L_INPUT:特別保护 = 1
		PRINTFORMW 将%CALLNAME:L_INPUT%纳入特别保护对象了
		GOTO DATA_LIST
	ELSEIF CFlag:L_INPUT:特別保护 == 1
		CFlag:L_INPUT:特別保护 = 0
		PRINTFORMW 取消了%CALLNAME:L_INPUT%的特别保护身份
		GOTO DATA_LIST
	ENDIF
ENDIF

; CALL JN_CONFIG




;-------------------------------------------------
; 各居民事件倾向
;-------------------------------------------------
@JN_CONFIG_EVENT
#DIM L_I
#DIM L_INPUT
$DATA_LIST

L_I = 0

DRAWLINE
PRINTFORML 请选择要变更的角色
DRAWLINE




CALL LIST_CHARA

L_INPUT = RESULT
IF L_INPUT != 1000
	IF TALENT:L_INPUT:NPC == 999
		PRINTFORMW 不能选择NPC
		GOTO DATA_LIST
	ELSEIF L_INPUT == 0
		PRINTFORMW 不能选择你自己
		GOTO DATA_LIST
	ENDIF
	L_I = L_INPUT
	$INPUT_LOOP2
	CLEARLINE LINECOUNT
	DRAWLINE
	PRINTFORML  {L_I} %CALLNAME:L_I% 请选择要变更的事件倾向。
	DRAWLINE
	PRINTFORM [0] 交流事件(
	IF  CFlag:L_I:居民事件倾向_交流 == 90
		PRINTFORM 很多
	ELSEIF CFlag:L_I:居民事件倾向_交流 == 60
		PRINTFORM 普通
	ELSEIF CFlag:L_I:居民事件倾向_交流 == 30
		PRINTFORM 很少
	ELSE
		PRINTFORM 不发生
	ENDIF
	PRINTFORML )
	PRINTFORM [1] 恋爱事件(
	IF  CFlag:L_I:居民事件倾向_恋爱 == 90
		PRINTFORM 很多
	ELSEIF CFlag:L_I:居民事件倾向_恋爱 == 60
		PRINTFORM 普通
	ELSEIF CFlag:L_I:居民事件倾向_恋爱 == 30
		PRINTFORM 很少
	ELSE
		PRINTFORM 不发生
	ENDIF
	PRINTFORML )
	PRINTFORM [2] 犯罪事件(
	IF  CFlag:L_I:居民事件倾向_犯罪 == 90
		PRINTFORM 很多
	ELSEIF CFlag:L_I:居民事件倾向_犯罪 == 60
		PRINTFORM 普通
	ELSEIF CFlag:L_I:居民事件倾向_犯罪 == 30
		PRINTFORM 很少
	ELSE
		PRINTFORM 不发生
	ENDIF
	PRINTFORML )
	PRINTFORM [3] 性的事件(
	IF  CFlag:L_I:居民事件倾向_性 == 90
		PRINTFORM 很多
	ELSEIF CFlag:L_I:居民事件倾向_性 == 60
		PRINTFORM 普通
	ELSEIF CFlag:L_I:居民事件倾向_性 == 30
		PRINTFORM 很少
	ELSE
		PRINTFORM 不发生
	ENDIF
	PRINTFORML )
	PRINTFORM [10] 居民交流上限(
	IF  CFlag:L_I:居民交流设定 == 4
		PRINTFORM 没有上限
	ELSEIF CFlag:L_I:居民交流设定 == 3
		PRINTFORM 恋人
	ELSEIF CFlag:L_I:居民交流设定 == 2
		PRINTFORM 亲友
	ELSEIF CFlag:L_I:居民交流设定 == 1
		PRINTFORM 友人
	ELSE
		PRINTFORM 不发生
	ENDIF
	PRINTFORML )
	PRINTFORML
	PRINTFORML [999] 返回
	PRINTFORMLC \@(L_I > 1) ? [1001]上个角色 # %" " * 16%\@
	PRINTFORMLC   %" " * 18%
	PRINTFORMLC \@(L_I < CHARANUM-1) ? [1003]下个角色 # %" " * 16%\@
	$INPUT_LOOP
	INPUT
	L_INPUT = RESULT


	SELECTCASE L_INPUT
	CASE 999
	CASE 10
		CFlag:L_I:(3950+L_INPUT) += 1
		SIF CFlag:L_I:(3950+L_INPUT) >= 5
			CFlag:L_I:(3950+L_INPUT) = 0
		GOTO INPUT_LOOP2
	CASE 1001
		IF (L_I > 1)
			L_I --
			GOTO INPUT_LOOP2
		ENDIF
		GOTO INPUT_LOOP
	CASE 1003
		IF (L_I < CHARANUM-1)
			L_I ++
			GOTO INPUT_LOOP2
		ENDIF
		GOTO INPUT_LOOP
	CASE L_INPUT
		CFlag:L_I:(3950+L_INPUT) += 30
		SIF CFlag:L_I:(3950+L_INPUT) >= 100
			CFlag:L_I:(3950+L_INPUT) = 0
		GOTO INPUT_LOOP2
	CASEELSE
		GOTO INPUT_LOOP
	ENDSELECT
ENDIF




;-------------------------------------------------
; デリート
;-------------------------------------------------
@JN_CONFIG_DELETE
#DIM L_I
#DIM L_INPUT
$DATA_LIST

L_I = 0

DRAWLINE
PRINTFORML 请选择要抹消的居民
DRAWLINE




CALL LIST_CHARA

L_INPUT = RESULT
IF L_INPUT != 1000
	IF TALENT:L_INPUT:NPC == 999
		PRINTFORMW 不能选择NPC
		GOTO DATA_LIST
	ELSEIF L_INPUT == 0
		PRINTFORMW 不能选择你自己
		GOTO DATA_LIST
	ENDIF
	L_I = L_INPUT
	PRINTFORML %CALLNAME:L_I%将被抹消身份，真的可以吗？
	PRINTFORML [0] 是
	PRINTFORML [999] 否
	$INPUT_LOOP
	INPUT
	L_INPUT = RESULT
	SELECTCASE L_INPUT
	CASE 999
	CASE 0
	PRINTFORML
	PRINTFORMW %CALLNAME:L_I%被抹消了
	DELCHARA  L_I
	CASEELSE
		GOTO INPUT_LOOP
	ENDSELECT
ENDIF









; CALL JN_CONFIG

;-------------------------------------------------
; 各交流居民
;-------------------------------------------------
@JN_CONFIG_COM
#DIM L_NPC
#DIM L_INPUT
#DIM L_T
#DIM L_NAME

L_T = TARGET
$COM_LIST

DRAWLINE
PRINTFORML 请选择要参照的角色
DRAWLINE
CALL LIST_CHARA
TARGET = RESULT

SIF RESULT == 1000
	RETURN

IF TALENT:RESULT:NPC == 999
	PRINTFORMW 不能选择NPC
	GOTO COM_LIST
ELSEIF RESULT == 0
	PRINTFORMW 不能选择你自己
	GOTO COM_LIST
ENDIF


$INPUT_LOOP0
CLEARLINE LINECOUNT
DRAWLINE
PRINTFORML  {TARGET} %CALLNAME:TARGET%
PRINTFORML 能变更交流对象的关注，姓名。
DRAWLINE
L_NPC = 0
PRINTFORML [-1] 解除关注
$INPUT_LOOP
IF CSTR:(3000+L_NPC) == ""
	CALL PRINT_LABEL("未登陆")
	PRINTL
ELSE
SIF CFLAG:3600 == L_NPC
	SETCOLOR 50,250,250
	PRINTFORM [{L_NPC}] %CSTR:(3000+L_NPC), 18 ,LEFT%
	IF CFLAG:(3030+L_NPC) == 1
		CALL PRINT_LABEL("男")
	ELSE
		CALL PRINT_LABEL("女")
	ENDIF
	SELECTCASE CFLAG:(3020+L_NPC)
	CASE 5
		CALL PRINT_LABEL("关系",,,"夫妇")
	CASE 4
		CALL PRINT_LABEL("关系",,,"恋人")
	CASE 3
		CALL PRINT_LABEL("关系",,,"亲友")
	CASE 2
		CALL PRINT_LABEL("关系",,,"友人")
	CASE 1
		CALL PRINT_LABEL("关系",,,"认识")
	CASE -5
		CALL PRINT_LABEL("关系",,,"爱人")
	ENDSELECT
	CALL PRINT_LABEL("好感度",,,TOSTR(CFLAG:(3010+L_NPC)), -3)
	CALL PRINT_LABEL("相性",,,TOSTR(CFLAG:(3050+L_NPC)), -3)
	CALL PRINT_LABEL("性行为回数",,,TOSTR(CFLAG:(3060+L_NPC)))
	PRINTFORML
ENDIF
RESETCOLOR
L_NPC += 1
SIF L_NPC < 10
	GOTO INPUT_LOOP
DRAWLINE
PRINTFORML
PRINTFORML
PRINTFORML [999] 返回
PRINTFORMLC \@(TARGET > 1) ? [1001]上个角色 # %" " * 16%\@
PRINTFORMLC   %" " * 18%
PRINTFORMLC \@(TARGET < CHARANUM-1) ? [1003]下个角色 # %" " * 16%\@
$INPUT_LOOP2
INPUT
L_INPUT = RESULT
SELECTCASE L_INPUT
CASE -1
	CFLAG:3600 = -1
	CALL PRINT_LABEL("关注解除")
	PRINTW
	GOTO INPUT_LOOP0
CASE 999
	TARGET = L_T
	; CALL JN_CONFIG
	RETURN
CASE 1001
	IF (TARGET > 1)
		TARGET --
		GOTO INPUT_LOOP0
	ENDIF
	GOTO INPUT_LOOP2
CASE 1003
	IF (TARGET < CHARANUM-1)
		TARGET ++
		GOTO INPUT_LOOP0
	ENDIF
	GOTO INPUT_LOOP2
CASEELSE
	CFLAG:3600 = RESULT
	CALL PRINT_LABEL(@"%CSTR:(3000+RESULT)%被关注了")
	PRINTL
	PRINTFORMW 关注的居民会随着交流事件的多寡，得到好感度，相性增加的修正。
	DRAWLINE
	CALL PRINT_LABEL(@"接下来是姓名变更，未输入代表不变更。")
	PRINTL
	DRAWLINE
	INPUTS
	IF RESULTS == ""
		;スルー
	ELSE
		SIF CFLAG:(3020+L_NPC) == 5
		CSTR:伴侣姓名 = %RESULTS%
		CSTR:(3000+RESULT) = %RESULTS%
		CALL PRINT_LABEL(@"姓名变更为%CSTR:(3000+RESULT)%")
		PRINTL
		SIF TALENT:孩子气 || TALENT:早熟
			CSTR:一人称 = %CALLNAME:TARGET%
	ENDIF
	GOTO INPUT_LOOP0
ENDSELECT









IF TALENT:已婚 == 1
	PRINTFORML [200] 变更丈夫的姓名
ENDIF
TARGET = L_T

;-------------------------------------------------
;职务变更
;-------------------------------------------------
@POSITION_CHANGE
#DIM L_INPUT
#DIM L_INPUT2
#DIM L_P

$INPUT_LOOP
CLEARLINE LINECOUNT

DRAWLINE
PRINTFORML 消费资金500000以变更居民的职务。
PRINTFORML 请从以下职务选择。
PRINTFORML
PRINTFORML [1]  - 无业
PRINTFORML [93] - 圣职者
PRINTFORML [94] - 女仆
PRINTFORML [95] - 学生
PRINTFORML [96] - 偶像
PRINTFORML [97] - 社会人
PRINTFORML [98] - 娼妓
PRINTFORML [99] - 治安维持局员
PRINTFORML
PRINTFORML
PRINTFORML [999] - 返回

INPUT
L_P = RESULT
L_INPUT = RESULT

SIF L_INPUT == 999
	RETURN

IF MONEY < 500000
	PRINTFORML
	PRINTFORML 资金不足。
	PRINTFORMW
ENDIF

PRINTFORML
PRINTFORML 请选择要变更职务的居民。
PRINTFORML




CALL LIST_CHARA

L_INPUT = RESULT
SIF L_INPUT == 1000
	RETURN
IF L_INPUT != 1000
	IF TALENT:L_INPUT:NPC == 999
		PRINTFORMW 不能选择NPC
		GOTO INPUT_LOOP
	ELSEIF L_INPUT == 0
		PRINTFORMW 不能选择你自己
		GOTO INPUT_LOOP
	ENDIF

	IF TALENT:L_INPUT:学生 != 0 && L_P != 95
		PRINTFORML 要保留学生的身份吗？
		PRINTFORML
		PRINTFORML [1] - 是
		PRINTFORML [2] - 否
		INPUT
		L_INPUT2 = RESULT
		SIF L_INPUT2 >= 2
			TALENT:L_INPUT:学生 = 0
	ELSE
		TALENT:L_INPUT:学生 = 0
	ENDIF
	TALENT:L_INPUT:圣职者 = 0
	TALENT:L_INPUT:女仆 = 0
	TALENT:L_INPUT:偶像 = 0
	TALENT:L_INPUT:社会人 = 0
	TALENT:L_INPUT:娼妓 = 0
	TALENT:L_INPUT:治安维持局员 = 0


	IF L_P == 95
		PRINTFORML 学生の身分を選択してください
		PRINTFORML
		PRINTFORML [1] - 普通学生
		PRINTFORML [2] - 大学生
		PRINTFORML [3] - 高校生
		PRINTFORML [4] - 中学生
		PRINTFORML [5] - 小学生
		INPUT
		L_INPUT2 = RESULT
		TALENT:L_INPUT:L_P = L_INPUT2
	ELSE
		TALENT:L_INPUT:L_P = 1
	ENDIF
	MONEY -= 500000
	PRINTFORML
	PRINTFORML 变更しました。
	PRINTFORMW
ENDIF









;-------------------------------------------------
;姓名变更
;-------------------------------------------------
@JN_CONFIG_N_C
#DIM L_INPUT
#DIM L_NAME



$DATA_LIST

DRAWLINE
PRINTFORML 请选择要变更姓名的居民。
DRAWLINE

CALL LIST_CHARA

L_INPUT = RESULT
L_NAME = RESULT
SIF L_INPUT == 1000
	RETURN
	; CALL JN_CONFIG
IF L_INPUT != 1000
	IF TALENT:L_INPUT:NPC == 999
		PRINTFORMW 不能选择NPC
		GOTO DATA_LIST
	ELSEIF L_INPUT == 0
		PRINTFORMW 不能选择你自己
		GOTO DATA_LIST
	ENDIF
	DRAWLINE
	PRINTL 请选择新的CALLNAME。
	PRINTL 输入空白时，会不做选择并返回。
	DRAWLINE
	PRINTL
	INPUTS
	IF RESULTS == ""
		;スルー
	ELSE
		CALLNAME:L_NAME = %RESULTS%
		SIF TALENT:L_NAME:孩子气 || TALENT:L_NAME:早熟
			CSTR:L_NAME:一人称 = %CALLNAME:L_NAME%
	ENDIF
ENDIF







; CALL JN_CONFIG
;-------------------------------------------------
;キャラ管理用キャラセレクト関数
;-------------------------------------------------
@MANAGE_CHARA_SELECT(MODE_NOW = "MNG_正式居民指定")
#DIMS         MODE_NOW
#DIM          MODE_ID
#DIM          L_INPUT
#DIM          NOW_GROUP = 1 ;ページ記憶用
#DIM  DYNAMIC ROOM_ID       ;
#DIMS DYNAMIC OP_RELOAD     ;初回以外のページ更新とリスト更新を抑制する
{
#DIMS CONST   MODE_NAME = "MNG_登陆_登陆抹消",

	"MNG_入居_一号室",
	"MNG_入居_二号室",
	"MNG_入居_三号室",
	"MNG_入居_四号室",
	"MNG_入居_五号室",
	"MNG_入居_六号室",
	"MNG_入居_七号室",
	"MNG_入居_八号室",
	"MNG_入居_九号室",
	"MNG_入居_十号室",

	"MNG_登陆_正式居民",
	"MNG_登陆_特別保护",
	"MNG_登陆_姓名变更",

	"MNG_转职_圣职者",
	"MNG_转职_女仆",
	"MNG_转职_偶像",
	"MNG_转职_社会人",
	"MNG_转职_局员",
	"MNG_转职_娼妓",

	"MNG_配属_学院学生",
	"MNG_配属_支援住宅",
	"MNG_配属_爱情宾馆店員",
	"MNG_配属_カジノ店員",

	"MNG_特殊_外見变更",
	"MNG_特殊_治療設備",
	"MNG_特殊_品種改良",
	"MNG_特殊_身体強化",
	"MNG_特殊_次世代創造",

	"MNG_登陆_交友制御",
	"MNG_登陆_服装变更",
	"MNG_登陆_範囲消去",
	"MNG_登陆_抹消中止"
}

{
#DIMS CONST   MEMO_STR = "登陆抹消の候補者を選択してください。",

	"一号室に入居させる人物を選択してください。",
	"二号室に入居させる人物を選択してください。",
	"三号室に入居させる人物を選択してください。",
	"四号室に入居させる人物を選択してください。",
	"五号室に入居させる人物を選択してください。",
	"六号室に入居させる人物を選択してください。",
	"七号室に入居させる人物を選択してください。",
	"八号室に入居させる人物を選択してください。",
	"九号室に入居させる人物を選択してください。",
	"十号室に入居させる人物を選択してください。",

	"観察状況を切り替える人物を選択してください。",
	"保护指定を切り替える人物を選択してください。",
	"名称变更の对象者を選択してください。",

	"转职の对象者を選択してください",
	"转职の对象者を選択してください",
	"转职の对象者を選択してください",
	"转职の对象者を選択してください",
	"转职の对象者を選択してください",
	"转职の对象者を選択してください",

	"学院への入学对象者を選択してください",
	"支援住宅住宅ンティアの候補者を選択してください",
	"爱情宾馆テル店員の候補者を選択してください",
	"高級カジノ店員の候補者を選択してください",

	"外見变更の对象者を選択してください",
	"治療の对象者を選択してください",
	"品種改良の对象者を選択してください",
	"身体強化の对象者を選択してください",
	"次世代創造の母体を選択してください",

	"事件设定の对象者を選択してください",
	"服装变更の对象者を選択してください",

	"登陆抹消範囲の開始値を指定してください",
	""
}
#DIM CONST INPUT_MODE_外見变更   = 1024
#DIM CONST INPUT_MODE_治療設備   = 1025
#DIM CONST INPUT_MODE_品種改良   = 1026
#DIM CONST INPUT_MODE_身体強化   = 1027
#DIM CONST INPUT_MODE_次世代創造 = 1028
#DIM CONST INPUT_MODE_抹消中止   = 1032


MODE_ID = FINDELEMENT(MODE_NAME, MODE_NOW)
SELECTCASE MODE_ID
CASE -1
	MODE_NOW '= "MNG_登陆_正式居民"
	MODE_ID = FINDELEMENT(MODE_NAME, MODE_NOW)
ENDSELECT
CLEARLINE LINECOUNT
CALL LIST_CHARA_STATUS_MAIN(0, NOW_GROUP, OP_RELOAD, MODE_NOW, MEMO_STR:MODE_ID)



L_INPUT = RESULT
OP_RELOAD '= "「ページ更新無し」「リスト更新無し」"
SELECTCASE L_INPUT
CASE 0
	NOW_GROUP = 1
	CALL MNG_DELETE_CHARA
	RETURN
CASE 1 TO CHARANUM - 1
	SELECTCASE MODE_NOW
	CASE "RPG_武器強化"
		CALL RPG_WEAPON_MENU(L_INPUT)
	CASE "MNG_登陆_正式居民"
		CALL MNG_CHARA_REGULAR(L_INPUT)
	CASE "MNG_登陆_特別保护"
		CALL MNG_CHARA_PROTECT(L_INPUT)
	CASE "MNG_登陆_姓名变更"
		CALL MNG_RENAME(L_INPUT)
	CASE "MNG_登陆_登陆抹消"
		IF TALENT:L_INPUT:NPC
			CFLAG:L_INPUT:登陆抹消 = 0
		ELSEIF CFLAG:L_INPUT:正式居民 || CFLAG:L_INPUT:特別保护 || CFLAG:L_INPUT:マンションID
			CFLAG:L_INPUT:登陆抹消 = 0
		ELSE
			CFLAG:L_INPUT:登陆抹消 = !CFLAG:L_INPUT:登陆抹消
		ENDIF
	CASE "MNG_登陆_範囲消去"
	CASE "MNG_登陆_抹消中止"
	CASE "MNG_登陆_交友制御"
		CALL MNG_CONFIG_EVENT(L_INPUT)
	CASE "MNG_登陆_服装变更"
		CALL MNG_CHANGE_CLOTHES(L_INPUT)
	CASE "MNG_特殊_外見变更"
		CALL MNG_CUSTOM_LOOKS(L_INPUT)
	CASE "MNG_特殊_治療設備"
		CALL MNG_CURE_SICK(L_INPUT)
	CASE "MNG_特殊_品種改良"
		CALL CITY_F_54_GI2(L_INPUT)
	CASE "MNG_特殊_身体強化"
		CALL LABORATORY_BOOST_ST(L_INPUT)
	CASE "MNG_特殊_次世代創造"
		IF CFLAG:L_INPUT:居民_回収済み数  <= CFLAG:L_INPUT:居民_継承
			PRINTFORMW 条件を満たしていません
		ELSE
			CALL LABORATORY_BREEDING_BABY_DESIGNING(L_INPUT)
			OP_RELOAD '= "「ページ更新無し」"
		ENDIF
	CASE "MNG_转职_圣职者", "MNG_转职_女仆", "MNG_转职_偶像", "MNG_转职_社会人", "MNG_转职_局员", "MNG_转职_娼妓"
		CALL MNG_JOB(L_INPUT, MODE_NOW)
	CASE "MNG_配属_学院学生", "MNG_配属_支援住宅", "MNG_配属_爱情宾馆店員", "MNG_配属_カジノ店員"
		CALL MNG_FACILITY(L_INPUT, MODE_NOW)
	CASEELSE
		IF STRCOUNT(MODE_NOW, "MNG_入居")
			;マンション入居処理
			ROOM_ID = MODE_ID
			CALL MNG_MOVING_ROOM(L_INPUT, ROOM_ID)
		ELSE

		ENDIF
	ENDSELECT
CASE INPUT_MODE_外見变更
	IF FLAG:エステ >= 1
		MODE_NOW '= MODE_NAME:(L_INPUT % 1000)
	ENDIF
CASE INPUT_MODE_治療設備
	IF FLAG:病院 >= 1
		MODE_NOW '= MODE_NAME:(L_INPUT % 1000)
	ENDIF
CASE INPUT_MODE_品種改良
	IF FLAG:品種改良施設 >= 1
		MODE_NOW '= MODE_NAME:(L_INPUT % 1000)
	ENDIF
CASE INPUT_MODE_身体強化, INPUT_MODE_次世代創造
	IF FLAG:研究施設 >= 1
		MODE_NOW '= MODE_NAME:(L_INPUT % 1000)
	ENDIF
CASE INPUT_MODE_抹消中止
	CVARSET CFLAG, GETNUM(CFLAG, "登陆抹消"), 0
	MODE_NOW '= MODE_NAME:0
CASE 1000 TO 1031
	MODE_NOW '= MODE_NAME:(L_INPUT % 1000)
CASEELSE
ENDSELECT
RESTART


@MNG_DELETE_CHARA
SIF FINDCHARA(CFLAG:登陆抹消, 1) == -1
	RETURN

PRINTFORMW 居民の登陆抹消処理を行います
FOR LOCAL, CHARANUM - 1, 0, -1
	SIF !CFLAG:LOCAL:登陆抹消
		CONTINUE
	PRINTFORML 　[%CALLNAME:LOCAL%]はあなたの管理下を離れました
	DELCHARA LOCAL
NEXT
WAIT


;-------------------------------------------------
;正式居民指定管理関数
;-------------------------------------------------
@MNG_CHARA_REGULAR(L_C_ID)
#DIM         L_C_ID               ;对象登陆番号
#DIM DYNAMIC LIST_REGULAR, 300    ;正式居民リスト
#DIM DYNAMIC CNT_REGULAR          ;正式居民キャラのカウント
#DIM         L_C_ID_REMOVE        ;解任对象登陆番号
IF L_C_ID == MASTER
	PRINTFORMW ≪「プレイヤー」、あなたのモニタリングは必要ありません≫
	RETURN
ELSEIF CFLAG:L_C_ID:正式居民
	CFLAG:L_C_ID:正式居民 = 0
	RETURN
ENDIF
CLEARLINE LINECOUNT
CALL INFO_STATUS_HEADER(L_C_ID)
CALL C_PICTURE_S(L_C_ID)
DRAWLINE
PRINTFORML 　現在のモニタリング对象
DRAWLINE
CALL SHOW_ALL_REGULAR(LIST_REGULAR, CNT_REGULAR, "「ボタン無し」")
DRAWLINE

IF CNT_REGULAR >= 20
;上限
	PRINTFORML 観察对象の人数が多すぎます。
	PRINTFORML 観察を中止するキャラを選択してください。
	PRINTFORML
	FOR LOCAL, 0, 20
		IF TALENT:(LIST_REGULAR:LOCAL):不在
			SETCOLOR COLOR("灰色")
			PRINT ---
			PRINTFORM [%CALLNAME:(LIST_REGULAR:LOCAL), 18%]
		ELSE
			PRINTBUTTON @"{LIST_REGULAR:LOCAL, 3}[%CALLNAME:(LIST_REGULAR:LOCAL), 18%]", LIST_REGULAR:LOCAL
		ENDIF
		RESETCOLOR
		PRINTFORM 　
		SIF LOCAL % 5 == 4
			PRINTFORML
	NEXT
	PRINTFORML
	PRINTFORML [999] - キャラ選択に返回
	DO
		INPUT
		L_C_ID_REMOVE = RESULT
		SIF L_C_ID_REMOVE == 999
			RETURN
		IF !INRANGE(L_C_ID_REMOVE, 1, CHARANUM - 1)
		ELSEIF TALENT:L_C_ID_REMOVE:不在
		ELSEIF CFLAG:L_C_ID_REMOVE:正式居民 != 1
		ELSE
			BREAK
		ENDIF
	LOOP 1
	CALL REGULAR_CHARA_REMOVE(L_C_ID_REMOVE)
ENDIF
CFLAG:L_C_ID:正式居民 = 1



;-------------------------------------------------
;正式居民指定解除関数
;-------------------------------------------------
@REGULAR_CHARA_REMOVE(L_C_ID)
#DIM         L_C_ID               ;観察对象キャラの登陆番号
SIF TALENT:L_C_ID:不在 == 0
	PRINTFORMW %CALLNAME:L_C_ID%の観察指定を解除します。
CFLAG:L_C_ID:正式居民 = 0



;-------------------------------------------------
;正式居民指定関数
;-------------------------------------------------
@REGULAR_CHARA_SET(L_C_ID)
#DIM         L_C_ID               ;観察对象キャラの登陆番号
IF CFLAG:L_C_ID:正式居民
	PRINTFORMW %CALLNAME:L_C_ID%は既に観察对象に指定されています。
	PRINTFORML 観察を中止しますか？
	PRINTFORML [1] - 中止する
	PRINTFORML [0] - 続行する
	DO
		INPUT
		SIF RESULT == 1
			BREAK
		SIF RESULT == 0
			RETURN
	LOOP 1
	CALL REGULAR_CHARA_REMOVE(L_C_ID)
	RETURN
ENDIF
PRINTFORMW %CALLNAME:L_C_ID%を観察对象に指定しました。
CFLAG:L_C_ID:正式居民 = 1



@SHOW_ALL_REGULAR(LIST_REGULAR, CNT_REGULAR, OP)
#DIM  REF LIST_REGULAR
#DIM  REF CNT_REGULAR
#DIM      C_ID
#DIMS     OP
VARSET LIST_REGULAR
VARSET CNT_REGULAR
IF STRCOUNT(OP, "「ボタン無し」")
	FOR C_ID, 1, CHARANUM
		SIF CFLAG:C_ID:正式居民 != 1
			CONTINUE
		SIF TALENT:C_ID:不在
			SETCOLOR COLOR("灰色")
		PRINTFORM [%CALLNAME:C_ID, 18%]
		RESETCOLOR
		SIF CNT_REGULAR % 5 == 4
			PRINTFORML
		LIST_REGULAR:CNT_REGULAR = C_ID
		CNT_REGULAR ++
	NEXT
	SIF (CNT_REGULAR - 1) % 5 != 4
		PRINTFORML
ELSE
	FOR C_ID, 1, CHARANUM
		SIF CFLAG:C_ID:正式居民 != 1
			CONTINUE
		IF TALENT:C_ID:不在
			SETCOLOR COLOR("灰色")
			PRINT ---
			PRINTFORM [%CALLNAME:C_ID, 18%]
		ELSE
			PRINTBUTTON @"{C_ID, 3}[%CALLNAME:C_ID, 18%]", C_ID
		ENDIF
		RESETCOLOR
		PRINTFORM 　
		SIF CNT_REGULAR % 5 == 4
			PRINTFORML
		LIST_REGULAR:CNT_REGULAR = C_ID
		CNT_REGULAR ++
	NEXT
	SIF (CNT_REGULAR - 1) % 5 != 4
		PRINTFORML
ENDIF





;-------------------------------------------------
;特別保护管理関数
;-------------------------------------------------
@MNG_CHARA_PROTECT(L_C_ID)
#DIM         L_C_ID               ;对象登陆番号
#DIM DYNAMIC LIST_PROTECT, 300    ;特別保护リスト
#DIM DYNAMIC CNT_PROTECT          ;特別保护キャラのカウント
#DIM         L_C_ID_REMOVE        ;解任对象登陆番号
IF L_C_ID == 0
	PRINTFORMW ≪「プレイヤー」、あなたは既に保护されています≫
	RETURN
ELSEIF CFLAG:L_C_ID:特別保护
	;解任
	CFLAG:L_C_ID:特別保护 = 0
	RETURN
ENDIF
CLEARLINE LINECOUNT
CALL INFO_STATUS_HEADER(L_C_ID)
CALL C_PICTURE_S(L_C_ID)
DRAWLINE
PRINTFORML 　現在の特別保护对象
DRAWLINE
CALL SHOW_ALL_PROTECT(LIST_PROTECT, CNT_PROTECT, "「ボタン無し」")
DRAWLINE

IF CNT_PROTECT >= 30
	;上限
	PRINTFORML 保护对象の人数が多すぎます。
	PRINTFORML 保护を中止するキャラを選択してください。
	PRINTFORML
	FOR LOCAL, 0, CNT_PROTECT
		IF TALENT:(LIST_PROTECT:LOCAL):不在
			SETCOLOR COLOR("灰色")
			PRINT ---
			PRINTFORM [%CALLNAME:(LIST_PROTECT:LOCAL), 18%]
		ELSE
			PRINTBUTTON @"{LIST_PROTECT:LOCAL, 3}[%CALLNAME:(LIST_PROTECT:LOCAL), 18%]", LIST_PROTECT:LOCAL
		ENDIF
		RESETCOLOR
		PRINTFORM 　
		SIF LOCAL % 5 == 4
			PRINTFORML
	NEXT
	SIF (CNT_PROTECT - 1) % 5 != 4
		PRINTFORML
	PRINTFORML
	PRINTFORML [999] - キャラ選択に返回
	DO
		INPUT
		L_C_ID_REMOVE = RESULT
		SIF L_C_ID_REMOVE == 999
			RETURN
		IF !INRANGE(L_C_ID_REMOVE, 1, CHARANUM - 1)
		ELSEIF TALENT:L_C_ID_REMOVE:不在
		ELSEIF CFLAG:L_C_ID_REMOVE:特別保护 != 1
		ELSE
			BREAK
		ENDIF
	LOOP 1
	CALL PROTECT_CHARA_REMOVE(L_C_ID_REMOVE)

ENDIF
CFLAG:L_C_ID:特別保护 = 1



;-------------------------------------------------
;特別保护解除関数
;-------------------------------------------------
@PROTECT_CHARA_REMOVE(L_C_ID)
#DIM         L_C_ID               ;保护对象キャラの登陆番号
SIF TALENT:L_C_ID:不在 == 0
	PRINTFORMW %CALLNAME:L_C_ID%を保护对象から外しました。
CFLAG:L_C_ID:特別保护 = 0


;-------------------------------------------------
;特別保护指定関数
;-------------------------------------------------
@PROTECT_CHARA_SET(L_C_ID)
#DIM         L_C_ID               ;保护对象キャラの登陆番号
IF CFLAG:L_C_ID:特別保护
	PRINTFORMW %CALLNAME:L_C_ID%は既に保护对象に指定されています。
	PRINTFORML 保护を中止しますか？
	PRINTFORML [1] - 中止する
	PRINTFORML [0] - 続行する
	DO
		INPUT
		SIF RESULT == 1
			BREAK
		SIF RESULT == 0
			RETURN
	LOOP 1
	CALL PROTECT_CHARA_REMOVE(L_C_ID)
	RETURN
ENDIF
PRINTFORMW %CALLNAME:L_C_ID%を保护对象に指定しました。
CFLAG:L_C_ID:特別保护 = 1



@SHOW_ALL_PROTECT(LIST_PROTECT, CNT_PROTECT, OP)
#DIM  REF LIST_PROTECT
#DIM  REF CNT_PROTECT
#DIM      C_ID
#DIMS     OP
VARSET LIST_PROTECT
VARSET CNT_PROTECT
IF STRCOUNT(OP, "「ボタン無し」")
	FOR C_ID, 1, CHARANUM
		SIF CFLAG:C_ID:特別保护 != 1
			CONTINUE
		SIF TALENT:C_ID:不在
			SETCOLOR COLOR("灰色")
		PRINTFORM [%CALLNAME:C_ID, 18%]
		RESETCOLOR
		SIF CNT_PROTECT % 5 == 4
			PRINTFORML
		LIST_PROTECT:CNT_PROTECT = C_ID
		CNT_PROTECT ++
	NEXT
	SIF (CNT_PROTECT - 1) % 5 != 4
		PRINTFORML
ELSE
	FOR C_ID, 1, CHARANUM
		SIF CFLAG:C_ID:特別保护 != 1
			CONTINUE
		IF TALENT:C_ID:不在
			SETCOLOR COLOR("灰色")
			PRINT ---
			PRINTFORM [%CALLNAME:C_ID, 18%]
		ELSE
			PRINTBUTTON @"{C_ID, 3}[%CALLNAME:C_ID, 18%]", C_ID
		ENDIF
		RESETCOLOR
		PRINTFORM 　
		SIF CNT_PROTECT % 5 == 4
			PRINTFORML
		LIST_PROTECT:CNT_PROTECT = C_ID
		CNT_PROTECT ++
	NEXT
	SIF (CNT_PROTECT - 1) % 5 != 4
		PRINTFORML
ENDIF





@MNG_FACILITY(L_C_ID, MODE_NOW)
#DIM          L_C_ID               ;对象キャラの登陆番号
#DIMS         MODE_NOW
#DIM  DYNAMIC TEMP_ID
#DIM  DYNAMIC FACI_ID
{
#DIMS CONST   MODE_NAME     =
	"MNG_配属_学院学生",
	"MNG_配属_支援住宅",
	"MNG_配属_爱情宾馆店員",
	"MNG_配属_カジノ店員"
}
{
#DIM          L_FACI_LIST     =
	1,
	7,
	50,
	53
}
#DIM  DYNAMIC L_CNT_WOKER, 4        ;各施設に配属済みのキャラ数
#DIM  CONST   L_CFLAG_START = 10000 ;施設用CFLAGの開始値
#DIM  CONST   L_CFLAG_END   = 10100 ;施設用CFLAGの終了値（このID自体は含まれない）
#DIM L_CHARA_ORDER                  ;对象キャラがすでに配属されている施設

TEMP_ID = FINDELEMENT(MODE_NAME, MODE_NOW)
SIF TEMP_ID == -1
	RETURN
FACI_ID = L_FACI_LIST:TEMP_ID
L_CHARA_ORDER = FINDELEMENT(CFLAG:L_C_ID:0, 1, L_CFLAG_START, L_CFLAG_END)
IF L_CHARA_ORDER == L_CFLAG_START + FACI_ID
	CFLAG:L_C_ID:(L_CFLAG_START + FACI_ID) = 0
	RETURN
ENDIF

CLEARLINE LINECOUNT
CALL INFO_STATUS_HEADER(L_C_ID)
CALL C_PICTURE_S(L_C_ID)
DRAWLINE
PRINTFORML 　現在の配属キャラ
DRAWLINE
CALL SHOW_ALL_WORKERS(L_FACI_LIST, L_CNT_WOKER, "「ボタン無し」", L_C_ID)
DRAWLINE
IF L_CNT_WOKER:TEMP_ID >= 5
	PRINTFORML %CFLAGNAME:(L_CFLAG_START + FACI_ID)%の人数が多すぎます
	PRINTFORML 解任するキャラを選択してください。
	FOR LOCAL, 1, CHARANUM
		SIF TALENT:LOCAL:不在
			CONTINUE
		SIF CFLAG:LOCAL:(L_CFLAG_START + FACI_ID) == 0
			CONTINUE
		PRINTFORML [{LOCAL, 3}] - %CALLNAME:LOCAL%
	NEXT
	PRINTFORML [999] - やめる
	DO
		INPUT
		LOCAL = RESULT
		SELECTCASE LOCAL
		CASE 999
			RETURN
		CASE 1 TO CHARANUM -1
			SIF TALENT:LOCAL:不在
				CONTINUE
			SIF CFLAG:LOCAL:(L_CFLAG_START + FACI_ID) == 0
				CONTINUE
			BREAK
		CASEELSE
			CONTINUE
		ENDSELECT
	LOOP 1
	CFLAG:LOCAL:(L_CFLAG_START + FACI_ID) = 0
ENDIF
SIF L_CHARA_ORDER > 0
	CFLAG:L_C_ID:L_CHARA_ORDER = 0
CFLAG:L_C_ID:(L_CFLAG_START + FACI_ID) = 1



@MNG_JOB(L_C_ID, MODE_NOW)
#DIM          L_C_ID               ;对象キャラの登陆番号
#DIMS         MODE_NOW
{
#DIMS CONST   MODE_NAME =
	"MNG_转职_圣职者",
	"MNG_转职_女仆",
	"MNG_转职_偶像",
	"MNG_转职_社会人",
	"MNG_转职_局员",
	"MNG_转职_娼妓"
}
{
#DIM  CONST   JOB_ID =
	93,
	94,
	96,
	97,
	99,
	98
}
#DIM DYNAMIC TEMP_ID
TEMP_ID = FINDELEMENT(MODE_NAME, MODE_NOW)
SIF TEMP_ID < 0
	RETURN
TEMP_ID = JOB_ID:TEMP_ID
IF MONEY < 500000
	PRINTFORMW 资金500000が必要です
	RETURN
ENDIF

CLEARLINE LINECOUNT
CALL INFO_STATUS_HEADER(L_C_ID)
DRAWLINE
CALL C_PICTURE_S(L_C_ID)


IF MODE_NOW == "MNG_转职_娼妓"
	DRAWLINE
	PRINTFORML 资金500000を消费して
	IF TALENT:L_C_ID:娼妓
		PRINTFORML %CALLNAME:L_C_ID%の業務内容を見直します
		PRINTFORML よろしいですか？
		PRINTFORML [1] - 売春をやめさせる
		PRINTFORML [0] - そのまま
	ELSE
		PRINTFORML %CALLNAME:L_C_ID%に性処理の業務を与えます
		PRINTFORML よろしいですか？
		PRINTFORML [1] - 売春させる
		PRINTFORML [0] - そのまま
	ENDIF
	DO
		INPUT
		SIF RESULT == 0
			RETURN
		SIF RESULT == 1
			BREAK
	LOOP 1
	IF TALENT:L_C_ID:娼妓
		PRINTFORMW %CALLNAME:L_C_ID%は売春から足を洗った
	ELSE
		PRINTFORMW %CALLNAME:L_C_ID%は春をひさぎはじめた
	ENDIF
	TALENT:L_C_ID:娼妓 = !TALENT:L_C_ID:娼妓
	MONEY -= 500000
	RETURN
ELSEIF TALENT:L_C_ID:TEMP_ID == 1
	RETURN
ENDIF
DRAWLINE
PRINTFORML 资金500000を消费して%CALLNAME:L_C_ID%に%TALENTNAME:TEMP_ID%としての業務を与えます
PRINTFORML よろしいですか？
PRINTFORML [1] - 转职実行
PRINTFORML [0] - やめる
DO
	INPUT
	SIF RESULT == 0
		RETURN
	SIF RESULT == 1
		BREAK
LOOP 1
TALENT:L_C_ID:圣职者 = 0
TALENT:L_C_ID:女仆 = 0
TALENT:L_C_ID:偶像 = 0
TALENT:L_C_ID:社会人 = 0
TALENT:L_C_ID:治安维持局员 = 0
TALENT:L_C_ID:TEMP_ID = 1
PRINTFORMW %CALLNAME:L_C_ID%は%TALENTNAME:TEMP_ID%になった
MONEY -= 500000



@MNG_MOVING_ROOM(C_ID, ROOM_ID)
#DIM C_ID
#DIM ROOM_ID
#DIM SWAP_ID
IF !INRANGE(ROOM_ID, 1, 10)
	RETURN
ELSEIF C_ID == MASTER
	RETURN
ELSEIF TALENT:C_ID:NPC
	RETURN
ELSEIF CFLAG:C_ID:居民_体格 == 1
	PRINTFORMW 子供は入居することができません。
	RETURN
ENDIF

;同じ部屋番号ならリセット
IF FLAG:(5000 + ROOM_ID) == CFLAG:C_ID:個別番号
	FLAG:(5000 + ROOM_ID) = 0
	CFLAG:C_ID:マンションID = 0
	RETURN
ELSEIF CFLAG:C_ID:マンションID == ROOM_ID
	FLAG:(5000 + CFLAG:C_ID:マンションID) = 0
	CFLAG:C_ID:マンションID = 0
	RETURN
ENDIF

;他のキャラが入居済みのとき
IF FLAG:(5000 + ROOM_ID)
	;該当キャラの検索
	SWAP_ID = FINDCHARA(CFLAG:個別番号, FLAG:(5000 + ROOM_ID), 1, CHARANUM)
	IF SWAP_ID >= 1
		CFLAG:SWAP_ID:マンションID = CFLAG:C_ID:マンションID
		;入居予定者が既に部屋に入っているなら入れ替え処理
		IF CFLAG:C_ID:マンションID
			FLAG:(5000 + CFLAG:C_ID:マンションID) = CFLAG:SWAP_ID:個別番号
			CFLAG:SWAP_ID:マンションID = CFLAG:C_ID:マンションID
		;そうでないなら部屋から出るだけ
		ELSE
			CFLAG:SWAP_ID:マンションID = 0
		ENDIF
	ENDIF
	FLAG:(5000 + ROOM_ID) = CFLAG:C_ID:個別番号
	CFLAG:C_ID:マンションID = ROOM_ID
ELSE
	FLAG:(5000 + CFLAG:C_ID:マンションID) = 0
	FLAG:(5000 + ROOM_ID) = CFLAG:C_ID:個別番号
	CFLAG:C_ID:マンションID = ROOM_ID
ENDIF



@MNG_CUSTOM_LOOKS(L_I)
#DIM         L_I
#DIM DYNAMIC L_CHANGE_H_COLOR = -1 ;
#DIM DYNAMIC L_CHANGE_H_STYLE = -1 ;えっちすたいるではなくヘアスタイルだ
#DIM DYNAMIC L_CHANGE_E_COLOR = -1 ;
#DIM DYNAMIC L_CHANGE_B_COLOR = -1 ;
#DIM         L_NOW_H_COLOR         ;画像表示用に一時变更する
#DIM         L_NOW_H_STYLE         ;
#DIM         L_NOW_E_COLOR         ;
#DIM         L_NOW_B_COLOR         ;
#DIM         L_PARTS_TYPE
IF !INRANGE(L_I, 0, CHARANUM - 1)
	RETURN
ELSEIF L_I == MASTER
	RETURN
ELSEIF TALENT:L_I:NPC
	RETURN
ELSEIF MONEY < 10000
	PRINTFORMW 资金が不足しています。
	RETURN
ENDIF
;变更前の状態を保存
L_NOW_H_COLOR = CFLAG:L_I:髪色
L_NOW_H_STYLE = CFLAG:L_I:髪型
L_NOW_E_COLOR = CFLAG:L_I:瞳色
L_NOW_B_COLOR = CFLAG:L_I:肌色

DO
	CLEARLINE LINECOUNT
	DRAWLINE
	PRINTFORML %CALLNAME:L_I%の容姿を变更します
{
	CALL PRINT_LABEL("髪色",,,@"%INFO_STR_HAIRCOLOR(L_NOW_H_COLOR), 20%→\@
					L_CHANGE_H_COLOR == -1 ? 变更なし # %INFO_STR_HAIRCOLOR(L_CHANGE_H_COLOR)%\@",,,,,,,, 1)
}
{
	CALL PRINT_LABEL("髪型",,,@"%INFO_STR_HAIR(L_NOW_H_STYLE), 20%→\@
					L_CHANGE_H_STYLE == -1 ? 变更なし # %INFO_STR_HAIR(L_CHANGE_H_STYLE)%\@",,,,,,,, 1)
}
{
	CALL PRINT_LABEL("瞳色",,,@"%INFO_STR_EYECOLOR(L_NOW_E_COLOR), 20%→\@
					L_CHANGE_E_COLOR == -1 ? 变更なし # %INFO_STR_EYECOLOR(L_CHANGE_E_COLOR)%\@",,,,,,,, 1)
}
{
	CALL PRINT_LABEL("肌色",,,@"%INFO_STR_SKINCOLOR(L_NOW_B_COLOR), 20%→\@
					L_CHANGE_B_COLOR == -1 ? 变更なし # %INFO_STR_SKINCOLOR(L_CHANGE_B_COLOR)%\@",,,,,,,, 1)
}
	DRAWLINE
	PRINTFORML 　　　　　
	PRINTFORML 　　　　　
	PRINTFORML 　　　　　
	PRINTFORML 　　　　　　→
	PRINTFORML 　　　　　
	PRINTFORML 　　　　　
	PRINTFORML 　　　　　
	CFLAG:L_I:髪色 = L_NOW_H_COLOR
	CFLAG:L_I:髪型 = L_NOW_H_STYLE
	CFLAG:L_I:瞳色 = L_NOW_E_COLOR
	CFLAG:L_I:肌色 = L_NOW_B_COLOR

	HTML_PRINT C_PICTURE_HTML(L_I, 0, -702, 0, 850)
	SIF L_CHANGE_H_COLOR > -1
		CFLAG:L_I:髪色 = L_CHANGE_H_COLOR
	SIF L_CHANGE_H_STYLE > -1
		CFLAG:L_I:髪型 = L_CHANGE_H_STYLE
	SIF L_CHANGE_E_COLOR > -1
		CFLAG:L_I:瞳色 = L_CHANGE_E_COLOR
	SIF L_CHANGE_B_COLOR > -1
		CFLAG:L_I:肌色 = L_CHANGE_B_COLOR
	HTML_PRINT C_PICTURE_HTML(L_I, 800, -800, 0, 850)
	DRAWLINE
	PRINTFORML [0] 变更のリセット
	DRAWLINE
	PRINTFORML [1] 髪色の变更
	IF FLAG:エステ >= 3
		PRINTFORML [2] 髪型の变更
	ELSE
		PRINTFORML  髪型の变更(レベル3以上必要)
	ENDIF
	IF FLAG:エステ >= 6
		PRINTFORML [3] 瞳色の变更
	ELSE
		PRINTFORML  瞳色の变更(レベル6以上必要)
	ENDIF
	IF FLAG:エステ >= 9
		PRINTFORML [4] 肌色の变更
	ELSE
		PRINTFORML  肌色の变更(レベル9以上必要)
	ENDIF
	PRINTFORML
	PRINTFORML [100] 適用　(￥10000)
	PRINTFORML [999] 返回
	INPUT
	SELECTCASE RESULT
	CASE 0
		L_CHANGE_H_COLOR = -1
		L_CHANGE_H_STYLE = -1
		L_CHANGE_E_COLOR = -1
		L_CHANGE_B_COLOR = -1
	CASE 1
		FOR L_PARTS_TYPE, 10, -1, -1
			PRINTFORML [{L_PARTS_TYPE, 2}] %INFO_STR_HAIRCOLOR(L_PARTS_TYPE)%
		NEXT
		PRINTFORML
		PRINTFORML [-1] 变更なし
		DO
			INPUT
			L_PARTS_TYPE = RESULT
			SIF INRANGE(L_PARTS_TYPE, -1, 10)
				BREAK
		LOOP 1
		L_CHANGE_H_COLOR = L_PARTS_TYPE
	CASE 2
		SIF FLAG:エステ < 3
			CONTINUE
		FOR L_PARTS_TYPE, 24, -1, -1
			PRINTFORML [{L_PARTS_TYPE, 2}] %INFO_STR_HAIR(L_PARTS_TYPE)%
		NEXT
		PRINTFORML
		PRINTFORML [-1] 变更なし
		DO
			INPUT
			L_PARTS_TYPE = RESULT
			SIF INRANGE(L_PARTS_TYPE, -1, 24)
				BREAK
		LOOP 1
		L_CHANGE_H_STYLE = L_PARTS_TYPE
	CASE 3
		SIF FLAG:エステ < 6
			CONTINUE
		FOR L_PARTS_TYPE, 5, -1, -1
			PRINTFORML [{L_PARTS_TYPE, 2}] %INFO_STR_EYECOLOR(L_PARTS_TYPE)%
		NEXT
		PRINTFORML
		PRINTFORML [-1] 变更なし
		DO
			INPUT
			L_PARTS_TYPE = RESULT
			SIF INRANGE(L_PARTS_TYPE, -1, 5)
				BREAK
		LOOP 1
		L_CHANGE_E_COLOR = L_PARTS_TYPE
	CASE 4
		SIF FLAG:エステ < 9
			CONTINUE
		FOR L_PARTS_TYPE, 5, -1, -1
			PRINTFORML [{L_PARTS_TYPE, 2}] %INFO_STR_SKINCOLOR(L_PARTS_TYPE)%
		NEXT
		PRINTFORML
		PRINTFORML [-1] 变更なし
		DO
			INPUT
			L_PARTS_TYPE = RESULT
			SIF INRANGE(L_PARTS_TYPE, -1, 5)
				BREAK
		LOOP 1
		L_CHANGE_B_COLOR = L_PARTS_TYPE
	CASE 100
		IF ALLSAMES(-1, L_CHANGE_H_COLOR, L_CHANGE_H_STYLE, L_CHANGE_E_COLOR, L_CHANGE_B_COLOR)
			PRINTFORMW 变更内容が设定されていません。
			CONTINUE
		;何らかの処理が適用される場合は個別に抜けるよ
		ELSEIF L_CHANGE_H_COLOR >= 0 && L_CHANGE_H_COLOR != L_NOW_H_COLOR
		ELSEIF L_CHANGE_H_STYLE >= 0 && L_CHANGE_H_STYLE != L_NOW_H_STYLE
		ELSEIF L_CHANGE_E_COLOR >= 0 && L_CHANGE_E_COLOR != L_NOW_E_COLOR
		ELSEIF L_CHANGE_B_COLOR >= 0 && L_CHANGE_B_COLOR != L_NOW_B_COLOR
		ELSE
			PRINTFORMW %CALLNAME:L_I%は既に指定の容姿になっています。
			CONTINUE
		ENDIF

		PRINTFORMW %CALLNAME:L_I%の容姿を变更しました。
		MONEY -= 10000
		RETURN
	CASE 999
		CFLAG:L_I:髪色 = L_NOW_H_COLOR
		CFLAG:L_I:髪型 = L_NOW_H_STYLE
		CFLAG:L_I:瞳色 = L_NOW_E_COLOR
		CFLAG:L_I:肌色 = L_NOW_B_COLOR
		RETURN
	ENDSELECT
LOOP 1



@MNG_CURE_SICK(L_I)
#DIM         L_I
#DIM         L_MONEY
#DIM         L_INPUT
IF !INRANGE(L_I, 0, CHARANUM - 1)
	RETURN
ELSEIF L_I == MASTER
	RETURN
ELSEIF TALENT:L_I:NPC
	RETURN
ELSEIF TALENT:L_I:快楽中毒 <= 0
	RETURN
ENDIF
L_MONEY = MIN(100000, 2000 + (EXP:L_I:売春経験 * 10))
CLEARLINE LINECOUNT
CALL INFO_STATUS_HEADER(L_I)
DRAWLINE
CALL C_PICTURE_S(L_I)
PRINTFORML %CALLNAME:L_I%の快楽中毒の治療には￥{L_MONEY}が必要です
PRINTFORML 治療いたしますか？
PRINTFORML
PRINTFORML [0] 是
PRINTFORML [1] 否
INPUT
L_INPUT = RESULT
SELECTCASE L_INPUT
CASE 0
	IF MONEY <= L_MONEY
		PRINTFORML 治療費が不足しています。
		RETURN
	ENDIF
	TALENT:L_I:快楽中毒 = 0
	PRINTFORMW %CALLNAME:L_I%を治療しました。
ENDSELECT



@MNG_CONFIG_EVENT(L_I)
#DIM        L_I
#DIM        L_INPUT
#DIMS CONST STR_EVE_RATE = "NG", "少", "並", "多"
#DIMS CONST STR_EVE_LIMIT = "NG", "友人", "亲友", "恋人", "無制限"
IF !INRANGE(L_I, 0, CHARANUM - 1)
	RETURN
ELSEIF L_I == MASTER
	RETURN
ELSEIF TALENT:L_I:NPC
	RETURN
ENDIF
CLEARLINE LINECOUNT
CALL INFO_STATUS_HEADER(L_I)
DRAWLINE
CALL C_PICTURE_S(L_I)
DRAWLINE
PRINTFORML 变更したい事件倾向を選択して下さい。
DRAWLINE
PRINTFORML  [0] 交流事件(頻度：%STR_EVE_RATE:(LIMIT(CFlag:L_I:居民事件倾向_交流 / 30, 0, 3))%)
PRINTFORML  [1] 恋爱事件(頻度：%STR_EVE_RATE:(LIMIT(CFlag:L_I:居民事件倾向_恋爱 / 30, 0, 3))%)
PRINTFORML  [2] 犯罪事件(頻度：%STR_EVE_RATE:(LIMIT(CFlag:L_I:居民事件倾向_犯罪 / 30, 0, 3))%)
PRINTFORML  [3] 性的事件(頻度：%STR_EVE_RATE:(LIMIT(CFlag:L_I:居民事件倾向_性 / 30, 0, 3))%)
PRINTFORML [10] 居民交流上限(上限：%STR_EVE_LIMIT:(LIMIT(CFlag:L_I:居民交流设定, 0, 4))%)
PRINTFORML [20] 交流对象の关注指定
PRINTFORML
PRINTFORML [999] 返回
INPUT
L_INPUT = RESULT
SELECTCASE L_INPUT
CASE 999
	RETURN
CASE 20
	CALL MNG_CONFIG_PARTNER(L_I)
CASE 10
	CFlag:L_I:(3950+L_INPUT) += 1
	SIF CFlag:L_I:(3950+L_INPUT) >= 5
		CFlag:L_I:(3950+L_INPUT) = 0
CASE 0, 1, 2, 3
	CFlag:L_I:(3950+L_INPUT) += 30
	SIF CFlag:L_I:(3950+L_INPUT) >= 100
		CFlag:L_I:(3950+L_INPUT) = 0
CASEELSE
ENDSELECT
RESTART



;-------------------------------------------------
; 各交流居民
;-------------------------------------------------
@MNG_CONFIG_PARTNER(L_I)
#DIM L_NPC
#DIM L_INPUT
#DIM L_I
#DIMS CONST NPC_LV = "爱人", "认识", "友人", "亲友", "恋人", "夫妇"
CLEARLINE LINECOUNT
CALL INFO_STATUS_HEADER(L_I)
DRAWLINE
CALL C_PICTURE_S(L_I)
DRAWLINE
PRINTFORML 交流对象の关注、姓名变更ができます。
DRAWLINE
PRINTFORML [-1] 关注解除
FOR L_NPC, 0, 10
	IF CSTR:L_I:(3000+L_NPC) == ""
		CALL PRINT_LABEL("未登陆")
		PRINTL
	ELSE
		SIF CFLAG:L_I:3600 == L_NPC
			SETCOLOR 50,250,250
		PRINTBUTTON @"[{10 + L_NPC}] 名称变更", 10 + L_NPC
		PRINTFORM 　
		PRINTBUTTON @"[{L_NPC}] %CSTR:L_I:(3000+L_NPC), 18 ,LEFT%", L_NPC
		IF CFLAG:L_I:(3030+L_NPC) == 1
			CALL PRINT_LABEL("男")
		ELSE
			CALL PRINT_LABEL("女")
		ENDIF
		SELECTCASE CFLAG:L_I:(3020+L_NPC)
		CASE 1 TO 5, -5
			CALL PRINT_LABEL("关系",,,NPC_LV:(MAX(0, CFLAG:L_I:(3020+L_NPC))))
		ENDSELECT
		CALL PRINT_LABEL("好感度",,,TOSTR(CFLAG:L_I:(3010+L_NPC)), -3)
		CALL PRINT_LABEL("相性",,,TOSTR(CFLAG:L_I:(3050+L_NPC)), -3)
		CALL PRINT_LABEL("性行为回数",,,TOSTR(CFLAG:L_I:(3060+L_NPC)))
		PRINTFORML
	ENDIF
	RESETCOLOR
NEXT
DRAWLINE
PRINTFORML [999] 返回
DO
	INPUT
	L_INPUT = RESULT
	SELECTCASE L_INPUT
	CASE -1
		CFLAG:L_I:3600 = -1
		CALL PRINT_LABEL("关注を解除しました")
		PRINTW
	CASE 999
		; CALL JN_CONFIG
		RETURN
	CASE 10 TO 19
		CALL PRINT_LABEL("交流对象の姓名を变更します、未入力で变更しません。")
		PRINTL
		DRAWLINE
		INPUTS
		IF RESULTS == ""
			;スルー
		ELSE
			L_NPC = L_INPUT % 10
			SIF CFLAG:L_I:(3020 + L_NPC) == 5
				CSTR:L_I:伴侣姓名 = %RESULTS%
			CSTR:L_I:(3000 + L_NPC) = %RESULTS%
			CALL PRINT_LABEL(@"姓名を%CSTR:L_I:(3000 + L_NPC)%に变更しました")
			PRINTL
			SIF TALENT:L_I:孩子气 || TALENT:L_I:早熟
				CSTR:L_I:一人称 = %CALLNAME:L_I%
		ENDIF
	CASE 0 TO 9
		SIF CSTR:L_I:(3000 + L_INPUT) == ""
			RESTART
		CFLAG:L_I:3600 = L_INPUT
		CALL PRINT_LABEL(@"%CSTR:L_I:(3000 + L_INPUT)%を关注にしました")
		PRINTL
		PRINTFORMW 关注にした居民とは交流发生が多くなり、好感度・相性増加に補正が入ります。
	CASEELSE
		CONTINUE
	ENDSELECT
	BREAK
LOOP 1
RESTART




@MNG_RENAME(C_ID)
#DIM          C_ID
#DIM          NAME_ID
#DIM          NAME_CNT
#DIMS DYNAMIC NAME_LIST, 30
#DIMS DYNAMIC HURIGANA_LIST, 30
IF !INRANGE(C_ID, 0, CHARANUM - 1)
	RETURN
ELSEIF C_ID == MASTER
	RETURN
ELSEIF TALENT:C_ID:NPC
	RETURN
ENDIF
CLEARLINE LINECOUNT
CALL INFO_STATUS_HEADER(C_ID)
DRAWLINE
CALL C_PICTURE_S(C_ID)
DRAWLINE
PRINTFORML 新しい姓名を選択してください（よみがなは記録されません）
DRAWLINE
CALL MAKE_NAMELIST(NAME_LIST, HURIGANA_LIST)
FOR NAME_ID, 0, 30
	PRINTBUTTON @"[{NAME_ID, 2}] %NAME_LIST:NAME_ID, 8% %HURIGANA_LIST:NAME_ID, 10% 同名{CNT_SAME_NAME(C_ID, NAME_LIST:NAME_ID), 3}", NAME_ID
	PRINTFORM 　
	SIF NAME_ID % 3 == 2
		PRINTFORML
NEXT
PRINTFORML
PRINTFORML [100] 自分で入力する
PRINTFORML [900] 姓名候補再生成
PRINTFORML [999] キャンセル
DO
	INPUT
	SELECTCASE RESULT
	CASE 999
		RETURN
	CASE 900
		RESTART
	CASE 100
		PRINTFORML 新しい姓名を入力してください
		INPUTS
		IF RESULTS == ""
			;スルー
			RESTART
		ELSE
			PRINTFORMW 姓名の变更が完了しました
			CALLNAME:C_ID '= RESULTS
			SIF TALENT:C_ID:孩子气 || TALENT:C_ID:早熟
				CSTR:C_ID:一人称 '= CALLNAME:C_ID
			RETURN
		ENDIF
	CASE 0 TO 29
		NAME_ID  = RESULT
		NAME_CNT = CNT_SAME_NAME(C_ID, NAME_LIST:NAME_ID)
		PRINTFORML %CALLNAME:C_ID%の新しい姓名は%NAME_LIST:NAME_ID%（%HURIGANA_LIST:NAME_ID%）となります
		SIF NAME_CNT
			PRINTFORML 同名のキャラは{NAME_CNT}人存在します
		PRINTFORML この姓名でよろしいですか？
		PRINTFORML [1] - %NAME_LIST:NAME_ID%で決定する
		PRINTFORML [0] - 考え直す
		DO
			INPUT
			SELECTCASE RESULT
			CASE 1
				PRINTFORMW 姓名の变更が完了しました
				CALLNAME:C_ID '= NAME_LIST:NAME_ID
				SIF TALENT:C_ID:孩子气 || TALENT:C_ID:早熟
					CSTR:C_ID:一人称 '= CALLNAME:C_ID
				RETURN
			CASE 0
				RESTART
			ENDSELECT
		LOOP 1
	CASEELSE
		CONTINUE
	ENDSELECT
LOOP 1





@CULC_COST_COS_CHANGE(MEMO_COS, NEW_COS, COS_PRICE)
#FUNCTION
#DIM REF     MEMO_COS
#DIM REF     NEW_COS
#DIM REF     COS_PRICE
#DIM DYNAMIC SUM_PRICE
FOR LOCAL, 0, 7
	SIF NEW_COS:LOCAL < 0
		CONTINUE
	SIF MEMO_COS:LOCAL == NEW_COS:LOCAL
		CONTINUE
	SUM_PRICE += COS_PRICE:LOCAL
NEXT
RETURNF SUM_PRICE



@MNG_CHANGE_CLOTHES(C_ID)
#DIM          C_ID
#DIMS CONST   COS_TYPE_ALL  = "服", "下着トップス", "下着ボトムス", "靴下", "靴", "帽子＆アクセ", "淫紋"
#DIM  CONST   COS_CFLAG_ID  = 420, 423, 424, 421, 422, 419, 418
#DIMS CONST   STR_BODY_SIZE   = "NORMAL", "MINI"
#DIM  DYNAMIC LOAD_CHECK = 1
#DIM  DYNAMIC MEMO_COS, 7
#DIM  DYNAMIC NEW_COS, 7
#DIM  DYNAMIC COS_PRICE, 7
IF LOAD_CHECK
	LOAD_CHECK = 0
	FOR LOCAL, 0, 7
		MEMO_COS:LOCAL = CFLAG:C_ID:(COS_CFLAG_ID:LOCAL)
		NEW_COS:LOCAL  = MEMO_COS:LOCAL
	NEXT
ENDIF
CLEARLINE LINECOUNT
CALL INFO_STATUS_HEADER(C_ID)
CALL C_PICTURE_FITTING(C_ID, MEMO_COS, NEW_COS)
PRINTFORML 着替えさせる服装の種類を選択してください(所持金:{MONEY})
DRAWLINE
FOR LOCAL, 0, VARSIZE("COS_TYPE_ALL")
	PRINTBUTTON @"[{LOCAL}] %COS_TYPE_ALL:LOCAL, 14%", LOCAL
	PRINT 　　｜
	IF MEMO_COS:LOCAL < 0
		PRINTFORM %"－－", 24, LEFT%　→
	ELSE
		PRINTFORM %COSTUME_DATA(STR_BODY_SIZE:(CFLAG:C_ID:居民_体格), COS_TYPE_ALL:LOCAL, MEMO_COS:LOCAL, "NAME"), 24, LEFT%　→
	ENDIF
	IF NEW_COS:LOCAL < 0
		PRINTFORM －－
	ELSEIF NEW_COS:LOCAL == MEMO_COS:LOCAL
		PRINTFORM そのまま
	ELSE
		PRINTFORM %COSTUME_DATA(STR_BODY_SIZE:(CFLAG:C_ID:居民_体格), COS_TYPE_ALL:LOCAL, NEW_COS:LOCAL, "NAME"), 24, LEFT%
	ENDIF
	PRINTL
NEXT
SIF !LINEISEMPTY()
	PRINTL
PRINTFORML
PRINTFORML [100] - 服装の購入 (費用{CULC_COST_COS_CHANGE(MEMO_COS, NEW_COS, COS_PRICE)})
PRINTFORML [200] - リセット
PRINTFORML [999] - キャンセル
DO
	INPUT
	SELECTCASE RESULT
	CASE 999
		FOR LOCAL, 0, 7
			CFLAG:C_ID:(COS_CFLAG_ID:LOCAL) = MEMO_COS:LOCAL
		NEXT
		RETURN
	CASE 100
		IF MONEY < CULC_COST_COS_CHANGE(MEMO_COS, NEW_COS, COS_PRICE)
			PRINTFORMW 所持金が足りません
		ELSE
			MONEY -= CULC_COST_COS_CHANGE(MEMO_COS, NEW_COS, COS_PRICE)
			FOR LOCAL, 0, 7
				MEMO_COS:LOCAL = CFLAG:C_ID:(COS_CFLAG_ID:LOCAL)
				NEW_COS:LOCAL  = MEMO_COS:LOCAL
			NEXT
			VARSET COS_PRICE
		ENDIF
		BREAK
	CASE 200
		FOR LOCAL, 0, 7
			NEW_COS:LOCAL  = MEMO_COS:LOCAL
		NEXT
		VARSET COS_PRICE
		BREAK
	CASE 0 TO 6
		CALL MNG_CLOTHES_FITTING(C_ID, COS_TYPE_ALL:RESULT, MEMO_COS, NEW_COS, COS_PRICE)
		BREAK
	ENDSELECT
LOOP 1
RESTART


@MNG_CLOTHES_FITTING(C_ID, COS_TYPE, MEMO_COS, NEW_COS, COS_PRICE)
#DIM          C_ID
#DIMS         COS_TYPE
#DIM  REF     MEMO_COS
#DIM  REF     NEW_COS
#DIM  REF     COS_PRICE
#DIMS CONST   COS_TYPE_ALL  = "服", "下着トップス", "下着ボトムス", "靴下", "靴", "帽子＆アクセ", "淫紋"
#DIM  CONST   COS_CFLAG_ID  = 420, 423, 424, 421, 422, 419, 418
#DIM  CONST   服_NORMAL_INDEX = 0, 1, 2, 3, 4, -1, 5, 6, 7, 8, 17, 18, 9, 10, 11, 12, 13, 14, 15, 16, 19, 20, 21, 22, 23, 24, -1, 25, 26, 27
#DIMS CONST   STR_BODY_SIZE   = "NORMAL", "MINI"
#DIMS         TEMP_LABEL
#DIM  DYNAMIC PRICE_LIST, 40
#DIM  DYNAMIC NOW_TYPE

NOW_TYPE = FINDELEMENT(COS_TYPE_ALL, COS_TYPE, , , 1)
SIF NOW_TYPE < 0
	RETURN

PRINTFORML
;先に例外処理
IF CFLAG:C_ID:居民_体格 == 0 && COS_TYPE == "服"
	FOR LOCAL, 0, VARSIZE("服_NORMAL_INDEX")
		IF 服_NORMAL_INDEX:LOCAL < 0
			PRINTFORM %" " * 39%
		ELSE
			PRICE_LIST:(服_NORMAL_INDEX:LOCAL) = TOINT(COSTUME_DATA(STR_BODY_SIZE:(CFLAG:C_ID:居民_体格), COS_TYPE, 服_NORMAL_INDEX:LOCAL, "PRICE"))
			TEMP_LABEL '= @"[{服_NORMAL_INDEX:LOCAL, 2}] "
			TEMP_LABEL += @"%COSTUME_DATA(STR_BODY_SIZE:(CFLAG:C_ID:居民_体格), COS_TYPE, 服_NORMAL_INDEX:LOCAL, "NAME"), 24, LEFT%"
			TEMP_LABEL += "　\\" + @"{PRICE_LIST:(服_NORMAL_INDEX:LOCAL), 7, RIGHT}"
			PRINTBUTTON TEMP_LABEL, 服_NORMAL_INDEX:LOCAL
		ENDIF
		PRINT 　
		SIF LOCAL % 3 == 2
			PRINTL
	NEXT
ELSEIF GROUPMATCH(COS_TYPE, "帽子＆アクセ", "淫紋")
	LOCAL = 1
	DO
		TEMP_LABEL '= @"%COSTUME_DATA(STR_BODY_SIZE:(CFLAG:C_ID:居民_体格), COS_TYPE, LOCAL, "NAME"), 24, LEFT%"
		SIF TEMP_LABEL == " " * 24
			BREAK
		PRICE_LIST:LOCAL = TOINT(COSTUME_DATA(STR_BODY_SIZE:(CFLAG:C_ID:居民_体格), COS_TYPE, LOCAL, "PRICE"))
		TEMP_LABEL '= TEMP_LABEL + "　\\" + @"{PRICE_LIST:LOCAL, 7, RIGHT}"
		TEMP_LABEL '= @"[{LOCAL, 2}] " + TEMP_LABEL
		PRINTBUTTON TEMP_LABEL, LOCAL
		PRINT 　
		SIF LOCAL % 4 == 3
			PRINTL
		LOCAL ++
	LOOP 1
ELSE
	LOCAL = 0
	DO
		TEMP_LABEL '= @"%COSTUME_DATA(STR_BODY_SIZE:(CFLAG:C_ID:居民_体格), COS_TYPE, LOCAL, "NAME"), 24, LEFT%"
		SIF TEMP_LABEL == " " * 24
			BREAK
		PRICE_LIST:LOCAL = TOINT(COSTUME_DATA(STR_BODY_SIZE:(CFLAG:C_ID:居民_体格), COS_TYPE, LOCAL, "PRICE"))
		TEMP_LABEL '= TEMP_LABEL + "　\\" + @"{PRICE_LIST:LOCAL, 7, RIGHT}"
		TEMP_LABEL '= @"[{LOCAL, 2}] " + TEMP_LABEL
		PRINTBUTTON TEMP_LABEL, LOCAL
		PRINT 　
		SIF LOCAL % 3 == 2
			PRINTL
		LOCAL ++
	LOOP 1
ENDIF
PRINTFORML
SIF !LINEISEMPTY()
	PRINTL
IF COS_TYPE == "淫紋"
	PRINTFORML [-1] 解除
ELSE
	PRINTFORML [-1] 脱がす
ENDIF
PRINTFORML [100] もどる
DO
	INPUT
	SELECTCASE RESULT
	CASE 100
		RETURN
	CASE -1
		CFLAG:C_ID:(COS_CFLAG_ID:NOW_TYPE) = -1
		NEW_COS:NOW_TYPE   = -1
		COS_PRICE:NOW_TYPE = 0
		RETURN
		[SKIPSTART]
		IF COS_TYPE == "淫紋"
			PRINTFORML 淫紋を解除します
		ELSE
			PRINTFORML 服装を脱がせます
		ENDIF
		PRINTFORML よろしいですか？
		PRINTFORML [1] 是
		PRINTFORML [0] 否
		INPUT
		IF RESULT == 1
			CFLAG:C_ID:(COS_CFLAG_ID:NOW_TYPE) = -1
			NEW_COS:NOW_TYPE   = -1
			COS_PRICE:NOW_TYPE = 0
			RETURN
		ENDIF
		[SKIPEND]
	CASE 0 TO 99
		LOCAL = RESULT
		SIF !STRLENS(COSTUME_DATA(STR_BODY_SIZE:(CFLAG:C_ID:居民_体格), COS_TYPE, LOCAL, "NAME"))
			CONTINUE

		CFLAG:C_ID:(COS_CFLAG_ID:NOW_TYPE) = LOCAL
		NEW_COS:NOW_TYPE   = LOCAL
		COS_PRICE:NOW_TYPE = PRICE_LIST:LOCAL
		RETURN
	ENDSELECT
LOOP 1

