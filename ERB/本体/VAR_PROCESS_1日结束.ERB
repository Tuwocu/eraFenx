;_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/#
;
;	Module		:VAR_PROCESS.ERB
;	Facility	:本体系処理群。
;
;	Licence		:ライセンスフリー。
;
;	Modification Data:
;
;	Edit	Date			Author					Reason
;	001		20XX/XX/XX		----					新規作成
;
;_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/#

;===============================================================================
;  ◇記載関数一覧(記載順)
;
;		Module Name						Explanation
;
; *- xxxx
;
;	・ VAR_TURNEND_MAIN					VAR:回合終了処理
;	・ VAR_TURNEND_INIT					VAR:回合終了処理(初期化)
;	・ VAR_DAYEND						VAR:一日終了処理
;	・ VAR_DAYEND_INIT					VAR:一日終了前処理
;
; *- その他
;
;	・ VAR_DEFAULT						VAR:開始処理(デフォルト)
;	・ VAR_INIT							VAR:初期化処理
;	・ VAR_RESTART						VAR:二週目処理
;
;===============================================================================

;-------------------------------------------------------------------------------
;   xxxx
;-------------------------------------------------------------------------------

;=================================================
;   sub VAR_TURNEND_MAIN
;=================================================
;   VAR:回合終了処理
;-------------------------------------------------
@VAR_TURNEND_MAIN
#DIM L_I
#DIM L_TMP
#DIM L_CHG_DAY

;- CHK:更新
SIF !(FLAG:一日结束旗标 || FLAG:回合结束旗标)
	RETURN

L_I=0
L_TMP=0

L_CHG_DAY = ( (FLAG:时段 >= [[时段:限定时间]]) ? 1 # 0)

SIF FLAG:一日结束旗标
	L_CHG_DAY = 1

;-----------------------------
;- 回合終了処理
;-----------------------------



;-------------------------------------------------

;-----------------------------
;- 一日終了処理
;-----------------------------
IF L_CHG_DAY
	;- VAR:一日終了前処理
	CALL VAR_DAYEND_INIT

	DRAWLINE
	;回合経過イベント
	CALL TURN_EVENT


	;- 妊娠:出産処理
	;CALL NINSIN_CHILD_BIRTH

	;- VAR:一日終了処理
	CALL VAR_DAYEND

ENDIF


;-----------------------------
;- 時間区分更新処理
;-----------------------------

DRAWLINE
;表示:日付
CALL INFO_DATE
PRINTL 结束了……
WAIT

;- 日付が変わった場合は初期化
IF L_CHG_DAY
	FLAG:时段 = 0
	DAY ++

	;- EVE:エンディング主処理
	CALL EVENT_ED_MAIN
ELSE
	FLAG:时段 ++
ENDIF

CALL TURN_EVENT_1

;表示:日付
CALL INFO_DATE
PRINTL 移动
WAIT

FLAG:一日结束旗标		= 0
FLAG:回合结束旗标	= 0

;-----------------------------
;- 全角色を処理
;-----------------------------
FOR L_I, 0, CHARANUM

	;- VAR:終了処理(初期化)
	CALL VAR_TURNEND_INIT(L_I)

NEXT

;-----------------------------
;- 追加処理
;-----------------------------

;- SYSTEM:オートセーブ処理
CALL SYS_AUTO_SAVE



;=================================================
;   sub VAR_TURNEND_INIT
;=================================================
;   VAR:回合終了処理(初期化)
;-------------------------------------------------
;   次回の行动用に各ステータスを回復させる
;-------------------------------------------------
; Input:
;  ARG					角色位置
;-------------------------------------------------
@VAR_TURNEND_INIT(ARG)
#DIM L_TMP

L_TMP=0

;-----------------------------
;-- 基本処理
;-----------------------------

;- 时段で処理分岐(％を設定)
SELECTCASE FLAG:时段
	;- 0:最初の行动時間(=日付変更時)
	CASE 0
		L_TMP		= 100
	;- その他
	CASE 1
		L_TMP		= 75
	CASE 2
		L_TMP		= 50
	CASEELSE
		L_TMP		= 25
ENDSELECT

;- 計算
BASE:ARG:体力	+= C_TIMES(MAXBASE:ARG:体力, L_TMP)

;- 丸め
BASE:ARG:体力 = MIN(BASE:ARG:体力,MAXBASE:ARG:体力)


;-----------------------------
;-- 追加処理
;-----------------------------




;=================================================
;   sub VAR_DAYEND
;=================================================
;   VAR:一日終了処理
;-------------------------------------------------
@VAR_DAYEND
#DIM L_I
#DIM L_T

FOR L_I, 0, CHARANUM
	L_T = L_I


	;初期化
	TALENT:L_T:排卵药 = 0
NEXT


;=================================================
;   sub VAR_DAYEND_INIT
;=================================================
;   VAR:一日終了前処理
;-------------------------------------------------
@VAR_DAYEND_INIT
#DIM L_I
#DIM L_T

FOR L_I, 0, CHARANUM
	L_T = L_I

	;初期化
	CALL MNG_ITEM_RENEW(L_T)
NEXT



;-------------------------------------------------------------------------------
;   その他
;-------------------------------------------------------------------------------

;=================================================
;   sub VAR_DEFAULT
;=================================================
;   VAR:開始処理(デフォルト)
;-------------------------------------------------
@VAR_DEFAULT

DAY = 1
MONEY = 0

;-----------------------------
;-- FLAG
;-----------------------------

FLAG:定数_角色表示数		= 15
FLAG:STATUS隐藏模式		= 1
FLAG:纯爱模式			= 1
FLAG:NTR模式			= 1


;=================================================
;   sub VAR_INIT
;=================================================
;   VAR:初期化処理
;-------------------------------------------------
@VAR_INIT

;-----------------------------
;-- FLAG
;-----------------------------

FLAG:最大数_角色			= DEFSMN_LIST_NUM
FLAG:最大数_JUEL			= 99999
FLAG:最大数_PALAM			= 99999
FLAG:最大数_PALAMLV			= 9
FLAG:最大数_ABLLV			= 9
FLAG:最大数_MARKLV			= 3
FLAG:最大数_EXP				= 999

FLAG:最大数_心情			= 1000
FLAG:最大数_心情位数		= 5
FLAG:最大数_调教人数		= 2
FLAG:最大值_绝顶			= 10000
FLAG:最大值_体力			= 900
FLAG:最大值_子宫精液		= 10

;※回合は0始まり
FLAG:最大数_行动限界回合	= 100 - 1

FLAG:定数_体力消费			= -10
FLAG:劳动最大值				= 9999


;-----------------------------
;- その他
;-----------------------------

GVAR_MONEY_NAME				= 资金


;-----------------------------
;- 追加処理
;-----------------------------
VARSET LOCAL

REPEAT CHARANUM
	LOCAL = NO:COUNT
	CFLAG:COUNT:使用口上 = LOCAL

;-------------------------------------------------
;	;存在チェック
;	IF EXISTCSV(LOCAL,0)
;		;使用口上を常にCSVに合わせる(暫定)
;		CFLAG:COUNT:使用口上 = CSVCFLAG(LOCAL, GETNUM(CFLAG, "使用口上"), 0)
;		SIF !CFLAG:COUNT:使用口上
;			CFLAG:COUNT:使用口上 = LOCAL
;	ENDIF
;-------------------------------------------------
REND





;=================================================
;   sub VAR_RESTART
;=================================================
;   VAR:二週目処理
;-------------------------------------------------
@VAR_RESTART
#DIM L_I
#DIM L_T

;-----------------------------
;- 変数類
;-----------------------------

;- 初期化
DAY = 1
TIME = 0
SIF MONEY < 500
	MONEY = 500

;奴隷ストック
VARSET GSMN_STOCK

;失踪角色のデータ消去
CALL 中古処理初期化

;-----------------------------
;- 角色引き継ぎ処理
;-----------------------------
FOR L_I, CHARANUM-1, -1, -1
	L_T = L_I

	SIF L_T == MASTER
		CONTINUE

	;引き継ぎ対象以外は削除
	IF (MARK:L_T:隶属刻印 < 3) || (MARK:L_T:陷落刻印 < 3)

		DELCHARA L_I
		CONTINUE

	;残す
	ELSE
		;初期化
		CFLAG:L_T:放置日数 = 0
	ENDIF

NEXT
